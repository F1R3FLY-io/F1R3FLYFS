plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'net.researchgate.release' version '3.0.2'
    id 'io.github.gradle-nexus.publish-plugin' version '1.2.0'
    id 'maven-publish'
    id 'signing'
    id 'java'
    id 'com.google.protobuf' version '0.8.18'
}


 

repositories {
    jcenter()
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.named('sourcesJar').configure {
    dependsOn ':generateProto'
}

shadowJar {
    archiveClassifier = 'shadow'
    relocate 'org.objectweb.asm', 'org.objectweb.asm.shaded'
}

// tasks.withType(Javadoc).configureEach {
//     options.addStringOption('Xdoclint:none', '-quiet')
// }

tasks.withType(Javadoc).all { enabled = false }

dependencies {
    // Gson dependency for JSON serialization/deserialization
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.bitcoinj:bitcoinj-core:0.16.2'
    // https://mvnrepository.com/artifact/com.github.jnr/jnr-posix
    implementation group: 'com.github.jnr', name: 'jnr-posix', version: '3.1.18'

    // Existing dependencies...
    
    // Bouncy Castle PKIX/CMS/EAC/PKCS/OCSP/TSP/OPENSSL
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    

    // gRPC
    implementation 'io.grpc:grpc-netty-shaded:1.45.1'
    implementation 'io.grpc:grpc-protobuf:1.61.0'
    implementation 'io.grpc:grpc-stub:1.45.1'
    implementation 'io.grpc:grpc-core:1.45.1'

    implementation 'com.google.api.grpc:proto-google-common-protos:1.17.0'


    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-protobuf:2.13.1.1'
    // Protobuf
    implementation 'com.google.protobuf:protobuf-java:3.19.4'
    implementation 'com.google.protobuf:protobuf-java-util:3.19.4'
    // gRPC services (if you need server-side)
    implementation 'io.grpc:grpc-services:1.45.1'

    // Web3J
implementation group: 'org.web3j', name: 'crypto', version: '4.10.3'

    testImplementation 'junit:junit:4.13.2'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'com.google.protobuf') {
            details.useVersion '3.19.4'
        }
    }
}

release {
    newVersionCommitMessage = '[jnr-fuse release] - '
    preTagCommitMessage = '[jnr-fuse release] - pre tag commit: '
    buildTasks = ['clean', 'assemble', 'publishToSonatype', 'closeAndReleaseSonatypeStagingRepository']
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jnr-fuse'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'jnr-fuse'
                description = 'jnr-fuse is a FUSE implementation in java using Java Native Runtime (JNR).'
                url = 'https://github.com/SerCeMan/jnr-fuse'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'SerCe'
                        name = 'Sergey Tselovalnikov'
                        email = 'sergeicelov@gmail.com'
                    }
                }

                scm {
                    url = 'https://github.com/SerCeMan/jnr-fuse'
                    connection = 'scm:git:git@github.com:SerCeMan/jnr-fuse.git'
                    developerConnection = 'scm:git:git@github.com:SerCeMan/jnr-fuse.git'
                }
            }
        }
    }

    repositories {
        maven {
            // A test repository in the build repository
            // allowing easy verification of published artifacts
            name = 'projectLocal'
            url = file("${buildDir}/repo")
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username.set(providers.environmentVariable("SONATYPE_LOGIN").forUseAtConfigurationTime())
            password.set(providers.environmentVariable("SONATYPE_PASSWORD").forUseAtConfigurationTime())
        }
    }
}

signing {
    sign publishing.publications.mavenJava
    setRequired {
        gradle.taskGraph.allTasks.any {
            it.name == 'publishToSonatype'
        }
    }
}
// Protobuf configuration
protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.19.4'
    }
    plugins {
         grpc {
             artifact = 'io.grpc:protoc-gen-grpc-java:1.45.1'
         }
     }
    generateProtoTasks {
        all()*.plugins {
             grpc {}
         }
    }
}

sourceSets {

    main {
        proto {
            // Include the 'src/main/proto' directory for proto files
            //srcDir 'src/main/proto'
            // Include the 'src/main/protobuf_external' directory for proto files
            //srcDir 'src/main/protobuf_external'
            srcDir '../f1r3fly/node/target/protobuf_external'
            srcDir '../f1r3fly/models/src/main/protobuf'
            srcDir '../f1r3fly/node/target/protobuf_external/scalapb'
        }
        java {
            // Specify the directory for the generated Java files
            srcDir 'build/generated/source/proto/main/java'
        }
    }
}

